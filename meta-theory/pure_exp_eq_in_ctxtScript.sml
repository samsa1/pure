(*
  Formalize the notion of context and a new relation coming with it
*)

open HolKernel Parse boolLib bossLib term_tactic;
open arithmeticTheory listTheory stringTheory alistTheory dep_rewrite
     optionTheory pairTheory ltreeTheory llistTheory bagTheory
     BasicProvers pred_setTheory relationTheory rich_listTheory finite_mapTheory;
open pure_expTheory pure_valueTheory pure_evalTheory pure_eval_lemmasTheory
     pure_exp_lemmasTheory pure_miscTheory pure_exp_relTheory pure_congruenceTheory;
open  pure_alpha_equivTheory;

val _ = new_theory "pure_exp_eq_in_ctxt";

(* Preliminaries *)

Theorem exp_eq_l_refl:
  âˆ€b l. LIST_REL (Î»x y. (x â‰…? y) b) l l
Proof
  gen_tac
  \\ Induct
  \\ fs [exp_eq_refl]
QED

Theorem exp_eq_Apps_cong:
  âˆ€l l' b e e'. LIST_REL (Î»x y. (x â‰…? y) b) l l' â‡’ (e â‰…? e') b â‡’ (Apps e l â‰…? Apps e' l') b
Proof
  Induct
  \\ fs [Apps_def]
  \\ rw [Apps_def]
  \\ fs [Apps_def]
  \\ first_x_assum $ irule
  \\ fs [exp_eq_App_cong]
QED

Theorem exp_eq_Lams_cong:
  âˆ€l e e' b. (e â‰…? e') b â‡’ (Lams l e â‰…? Lams l e') b
Proof
  Induct
  \\ rw [Lams_def]
  \\ fs [exp_eq_Lam_cong]
QED

Theorem eval_Prim:
  âˆ€ope eL eL'. LIST_REL (Î»e1 e2. eval e1 = eval e2) eL eL' â‡’ eval (Prim ope eL) = eval (Prim ope eL')
Proof
  Cases
  \\ rw [eval_thm]
  >~[â€˜MAP _ _ = MAP _ _â€™]
  >- (irule LIST_EQ
      \\ gvs [LIST_REL_EL_EQN, EL_MAP])
  >~[â€˜AtomOpâ€™]
  >- (fs [eval_def]
      \\ once_rewrite_tac [v_unfold]
      \\ fs [eval_wh_Prim]
      \\ qsuff_tac â€˜get_atoms (MAP eval_wh eL') = get_atoms (MAP eval_wh eL)â€™
      >- (CASE_TAC \\ fs [])
      \\ fs [get_atoms_def]
      \\ qsuff_tac â€˜EXISTS error_Atom (MAP eval_wh eL) â‡” EXISTS error_Atom (MAP eval_wh eL')â€™
      >- (strip_tac
          \\ IF_CASES_TAC
          \\ simp []
          \\ qsuff_tac â€˜MEM wh_Diverge (MAP eval_wh eL) â‡” MEM wh_Diverge (MAP eval_wh eL')â€™
          >- (strip_tac
              \\ IF_CASES_TAC
              \\ simp []
              \\ irule LIST_EQ
              \\ rw []
              \\ gvs [LIST_REL_EL_EQN, EL_MAP, EVERY_EL]
              \\ rpt $ first_x_assum drule
              \\ once_rewrite_tac [v_unfold]
              \\ rpt FULL_CASE_TAC
              \\ rw [dest_Atom_def, error_Atom_def])
          \\ eq_tac
          \\ strip_tac
          \\ gvs [MEM_EL, LIST_REL_EL_EQN]
          \\ first_assum $ irule_at Any
          \\ first_assum drule
          \\ once_rewrite_tac [v_unfold]
          \\ rpt FULL_CASE_TAC
          \\ gvs [EL_MAP])
      \\ eq_tac
      \\ strip_tac
      \\ gvs [EXISTS_MEM, MEM_EL, EL_MAP, LIST_REL_EL_EQN]
      \\ rename1 â€˜MAP eval_wh eL2â€™
      \\ qexists_tac â€˜eval_wh (EL n eL2)â€™
      \\ first_x_assum drule
      \\ once_rewrite_tac [v_unfold]
      \\ rpt FULL_CASE_TAC
      \\ fs [error_Atom_def]
      \\ rw []
      \\ first_assum $ irule_at Any
      \\ fs [EL_MAP])
  \\ Cases_on â€˜eLâ€™ \\ Cases_on â€˜eL'â€™ \\ fs []
  \\ rename1 â€˜LIST_REL _ t1 t2â€™
  \\ Cases_on â€˜t1â€™ \\ Cases_on â€˜t2â€™ \\ gvs [eval_thm]
  >~[â€˜Prim Seq (_::_::_)â€™]
  >- (rename1 â€˜LIST_REL _ t1 t2â€™
      \\ Cases_on â€˜t1â€™ \\ Cases_on â€˜t2â€™
      \\ gvs [eval_thm, eval_def]
      \\ once_rewrite_tac [v_unfold]
      \\ fs [eval_wh_Prim])
  >~[â€˜Prim If (_::_::_)â€™]
  >- (rename1 â€˜LIST_REL _ t1 t2â€™
      \\ Cases_on â€˜t1â€™ \\ Cases_on â€˜t2â€™ \\ gvs []
      >~[â€˜_::_::_::_â€™]
      >- (rename1 â€˜LIST_REL _ t1 t2â€™
          \\ Cases_on â€˜t1â€™ \\ Cases_on â€˜t2â€™
          \\ gvs [eval_thm, eval_def]
          \\ once_rewrite_tac [v_unfold]
          \\ rw [eval_wh_Prim])
      \\ rw [eval_def]
      \\ once_rewrite_tac [v_unfold]
      \\ rw [eval_wh_Prim])
  \\ rw [eval_def]
  \\ once_rewrite_tac [v_unfold]
  \\ rw [eval_wh_Prim]
QED

Theorem FLOOKUP_LUPDATE:
  âˆ€l f n v. FLOOKUP (f |++ l) n = SOME v â‡’ MEM (n, v) l âˆ¨ FLOOKUP f n = SOME v
Proof
  Induct
  \\ fs [FUPDATE_LIST_THM]
  \\ PairCases \\ rw []
  \\ first_x_assum $ dxrule_then assume_tac
  \\ gvs [FLOOKUP_UPDATE]
  \\ FULL_CASE_TAC \\ fs []
QED

Theorem Letrec_Prim:
  âˆ€l ope eL b. (Letrec l (Prim ope eL) â‰…? Prim ope (MAP (Letrec l) eL)) b
Proof
  rw []
  \\ irule eval_IMP_exp_eq
  \\ rw [subst_def, eval_thm, subst_funs_def, bind_def]
  >- (irule eval_Prim
      \\ rw [LIST_REL_EL_EQN, EL_MAP, subst_def, eval_thm, subst_funs_def]
      \\ gvs [bind_def]
      \\ IF_CASES_TAC \\ fs []
      \\ first_x_assum dxrule
      \\ strip_tac
      \\ fs [])
  \\ fs [MAP_MAP_o]
  \\ dxrule_then assume_tac FLOOKUP_LUPDATE
  \\ gvs [FLOOKUP_EMPTY, MEM_EL, EL_MAP]
  \\ qsuff_tac â€˜Fâ€™ \\ fs []
  \\ first_x_assum irule
  \\ rename1 â€˜EL k lâ€™
  \\ qabbrev_tac â€˜p = EL k lâ€™
  \\ PairCases_on â€˜pâ€™
  \\ gvs [EVERY_EL, EL_MAP]
  \\ rw []
  \\ rename1 â€˜EL k2 lâ€™
  \\ qabbrev_tac â€˜p' = EL k2 lâ€™
  \\ PairCases_on â€˜p'â€™
  \\ gvs []
  \\ â€˜âˆ€v. v âˆˆ FRANGE (FDIFF f (set (MAP FST l))) â‡’ closed vâ€™
    by (rw []
        \\ first_x_assum irule
        \\ gvs [FRANGE_FLOOKUP, FLOOKUP_FDIFF]
        \\ pop_assum $ irule_at Any)
  \\ gvs [freevars_subst, SUBSET_DEF, IN_DIFF, FDOM_FDIFF]
  \\ rw [MEM_EL]
  >>~[â€˜EL _ (MAP FST _) = EL _ (MAP FST _)â€™]
  >- (first_assum $ irule_at Any
      \\ gvs [EL_MAP]
      \\ rename1 â€˜FST p = FST _â€™
      \\ PairCases_on â€˜pâ€™
      \\ fs [])
  >- (first_assum $ irule_at Any
      \\ gvs [EL_MAP]
      \\ rename1 â€˜FST p = FST _â€™
      \\ PairCases_on â€˜pâ€™
      \\ fs [])
  \\ first_x_assum $ qspecl_then [â€˜xâ€™] assume_tac
  \\ pop_assum kall_tac
  \\ first_x_assum $ qspecl_then [â€˜xâ€™] assume_tac
  \\ gvs [] >>~[â€˜MEM x (MAP FST l)â€™]
  >- (gvs [MEM_EL]
      \\ first_assum $ irule_at Any
      \\ fs [EL_MAP]
      \\ rename1 â€˜FST p = FST _â€™
      \\ PairCases_on â€˜pâ€™ \\ fs [])
  >- (gvs [MEM_EL]
      \\ first_assum $ irule_at Any
      \\ fs [EL_MAP]
      \\ rename1 â€˜FST p = FST _â€™
      \\ PairCases_on â€˜pâ€™ \\ fs [])
  \\ first_x_assum $ qspecl_then [â€˜freevars p'1â€™] assume_tac
  \\ gvs [MEM_MAP]
  \\ pop_assum $ qspecl_then [â€˜EL k2 lâ€™] assume_tac
  \\ gvs [EL_MEM]
QED

Theorem Let_Lam:
  âˆ€v w e1 e2 b. closed e1 âˆ§ v â‰  w â‡’ (Let v e1 (Lam w e2) â‰…? Lam w (Let v e1 e2)) b
Proof
  rw []
  \\ irule exp_eq_trans
  \\ irule_at Any beta_equality
  \\ gvs [subst1_def]
  \\ irule exp_eq_Lam_cong
  \\ irule $ iffLR exp_eq_sym
  \\ irule beta_equality
  \\ gvs []
QED

Theorem Let_Lam_weak:
  âˆ€v w e1 e2 b. v â‰  w â‡’ w âˆ‰ freevars e1 â‡’ (Let v e1 (Lam w e2) â‰…? Lam w (Let v e1 e2)) b
Proof
  rw [exp_eq_def, bind_def] >> IF_CASES_TAC >>
  gvs [app_bisimilarity_eq, exp_eq_refl] >>
  rpt (irule_at Any IMP_closed_subst) >>
  drule_then assume_tac $ GSYM subst_fdomsub >>
  gvs [subst_def, DOMSUB_COMMUTES] >>
  irule_at Any Let_Lam >>
  irule_at Any IMP_closed_subst >>
  rw [FRANGE_FLOOKUP]
QED

Theorem FDIFF_DOMSUB:
  âˆ€f v s. FDIFF (f \\ s) v = (FDIFF f v) \\ s
Proof
  rw [FDIFF_def, fmap_domsub, INTER_COMM]
QED

Theorem MAP_subst_fdomsub:
  âˆ€v l f. EVERY (Î»e. v âˆ‰ freevars e) (MAP SND l) â‡’
          MAP (Î»(s, e). (s, subst (FDIFF f (set (MAP FST l)) \\ v) e)) l
          = MAP (Î»(s, e). (s, subst (FDIFF f (set (MAP FST l))) e)) l
Proof
  rw [EVERY_EL] >> irule LIST_EQ >>
  rw [EL_MAP] >> first_x_assum $ drule_then assume_tac >>
  rename1 â€˜EL x lâ€™ >> qabbrev_tac â€˜p = EL x lâ€™ >> PairCases_on â€˜pâ€™ >>
  gvs [subst_fdomsub, EL_MAP]
QED

Theorem Letrec_Lam:
  âˆ€l w e b. EVERY (Î»e. freevars e âŠ† set (MAP FST l)) (MAP SND l) âˆ§ Â¬MEM w (MAP FST l)
          â‡’ (Letrec l (Lam w e) â‰…? Lam w (Letrec l e)) b
Proof
  rw []
  \\ irule exp_eq_trans
  \\ irule_at Any beta_equality_Letrec
  \\ gvs [subst_funs_eq_subst, subst_def]
  \\ irule exp_eq_Lam_cong
  \\ irule $ iffLR exp_eq_sym
  \\ irule exp_eq_trans
  \\ irule_at Any beta_equality_Letrec
  \\ gvs [subst_funs_eq_subst]
  \\ irule eval_IMP_exp_eq
  \\ rw []
  \\ rpt AP_TERM_TAC \\ AP_THM_TAC \\ AP_TERM_TAC
  \\ irule $ GSYM DOMSUB_NOT_IN_DOM
  \\ gvs [FDOM_FUPDATE_LIST]
  \\ strip_tac \\ last_x_assum irule
  \\ gvs [MEM_EL]
  \\ first_assum $ irule_at Any
  \\ gvs [EL_MAP]
  \\ rename1 â€˜FST (_ p)â€™
  \\ PairCases_on â€˜pâ€™
  \\ fs []
QED

Theorem Letrec_Lam_weak:
  âˆ€l v e b. EVERY (Î»e. v âˆ‰ freevars e) (MAP SND l) âˆ§ Â¬ MEM v (MAP FST l)
            â‡’ (Letrec l (Lam v e) â‰…? Lam v (Letrec l e)) b
Proof
  rw [exp_eq_def, bind_def] >> IF_CASES_TAC >>
  gvs [app_bisimilarity_eq, exp_eq_refl] >>
  rpt $ irule_at Any IMP_closed_subst >>
  gvs [subst_def, FDIFF_DOMSUB, MAP_subst_fdomsub] >>
  irule_at Any Letrec_Lam >>
  gvs [MAP_MAP_o, combinTheory.o_DEF, LAMBDA_PROD, EVERY_EL, EL_MAP] >>
  rw [FRANGE_FLOOKUP]
  >- (rename1 â€˜EL n lâ€™ >> qabbrev_tac â€˜p = EL n lâ€™ >>
      PairCases_on â€˜pâ€™ >>
      â€˜âˆ€v. v âˆˆ FRANGE (FDIFF f (set (MAP FST l))) â‡’ closed vâ€™
        by (rw [FRANGE_FLOOKUP, FLOOKUP_FDIFF] >>
            first_x_assum irule >>
            pop_assum $ irule_at Any) >>
      gvs [freevars_subst, SUBSET_DEF, IN_DIFF, FDOM_FDIFF] >>
      gen_tac >> rename1 â€˜MEM x _â€™ >>
      last_x_assum $ qspecl_then [â€˜xâ€™] assume_tac >>
      last_x_assum $ qspecl_then [â€˜xâ€™] assume_tac >>
      rw [] >> gvs []
      >- (last_x_assum $ qspecl_then [â€˜freevars p1â€™] assume_tac >>
          gvs [MEM_MAP] >> pop_assum $ qspecl_then [â€˜EL n lâ€™] assume_tac >>
          gvs [EL_MEM])
      >- (last_x_assum $ qspecl_then [â€˜freevars p1â€™] assume_tac >>
          gvs [MEM_MAP] >> pop_assum $ qspecl_then [â€˜EL n lâ€™] assume_tac >>
          gvs [EL_MEM]) >>
      gvs [MEM_EL] >>
      first_assum $ irule_at Any >>
      rw [EL_MAP] >>
      rename1 â€˜FST p2â€™ >> PairCases_on â€˜p2â€™ >> fs [])
  >- (strip_tac >> last_x_assum irule >>
      gvs [MEM_EL] >> first_assum $ irule_at Any >>
      gvs [EL_MAP] >> rename1 â€˜FST pâ€™ >> PairCases_on â€˜pâ€™ >>
      fs [])
QED

Theorem Letrec_not_in_freevars:
  âˆ€l e b. EVERY (Î»v. v âˆ‰ freevars e) (MAP FST l) â‡’ (Letrec l e â‰…? e) b
Proof
  rw [exp_eq_def, bind_def] >> IF_CASES_TAC >>
  gvs [app_bisimilarity_eq, exp_eq_refl] >>
  rpt $ irule_at Any IMP_closed_subst >>
  gvs [subst_def, FRANGE_FLOOKUP] >>
  irule exp_eq_trans >>
  irule_at Any beta_equality_Letrec >>
  conj_asm1_tac
  >- (rw [EVERY_EL, EL_MAP, MAP_MAP_o, combinTheory.o_DEF, LAMBDA_PROD] >>
      rename1 â€˜EL n lâ€™ >> qabbrev_tac â€˜p = EL n lâ€™ >> PairCases_on â€˜pâ€™ >>
      â€˜âˆ€v. v âˆˆ FRANGE (FDIFF f (set (MAP FST l))) â‡’ closed vâ€™
        by (rw [FRANGE_FLOOKUP, FLOOKUP_FDIFF] >>
            first_x_assum irule >>
            pop_assum $ irule_at Any) >>
      gvs [freevars_subst, SUBSET_DEF, IN_DIFF, FDOM_FDIFF] >>
      gen_tac >> rename1 â€˜MEM x _â€™ >>
      last_x_assum $ qspecl_then [â€˜xâ€™] assume_tac >>
      rw [] >> gvs []
      >- (first_x_assum $ qspecl_then [â€˜freevars p1â€™] assume_tac >>
          gvs [MEM_MAP] >> pop_assum $ qspecl_then [â€˜EL n lâ€™] assume_tac >>
          gvs [EL_MEM]) >>
      gvs [MEM_EL] >>
      first_assum $ irule_at Any >> rw [EL_MAP] >>
      rename1 â€˜FST p2â€™ >> PairCases_on â€˜p2â€™ >> fs []) >>
  dxrule_then assume_tac subst_funs_eq_subst >>
  qspecl_then [â€˜fâ€™, â€˜eâ€™, â€˜set (MAP FST l)â€™] assume_tac $ GSYM subst_FDIFF' >>
  gvs [EVERY_MEM] >>
  rw [exp_eq_refl, IMP_closed_subst, FRANGE_FLOOKUP]
QED

Theorem Let_Letrec:
  âˆ€s l e1 e2 b. EVERY (Î»e. s âˆ‰ freevars e) (MAP SND l) âˆ§ Â¬MEM s (MAP FST l)
                âˆ§ EVERY (Î»v. v âˆ‰ freevars e1) (MAP FST l)
                â‡’ (Let s e1 (Letrec l e2) â‰…? Letrec l (Let s e1 e2)) b
Proof
  rw [] >>
  irule exp_eq_trans >>
  irule_at (Pos hd) exp_eq_App_cong >>
  irule_at (Pos hd) $ iffLR exp_eq_sym >>
  irule_at Any Letrec_Lam_weak >> fs [] >>
  irule_at Any exp_eq_refl >>
  irule $ iffLR exp_eq_sym >>
  irule exp_eq_trans >>
  irule_at Any Letrec_App >>
  irule exp_eq_App_cong >>
  gvs [exp_eq_refl, Letrec_not_in_freevars]
QED

Theorem Let_Let:
  âˆ€v w e1 e2 e3 b. v â‰  w âˆ§ v âˆ‰ freevars e2 âˆ§ w âˆ‰ freevars e1 â‡’
                   (Let v e1 (Let w e2 e3) â‰…? Let w e2 (Let v e1 e3)) b
Proof
  rw []
  \\ irule eval_IMP_exp_eq
  \\ rw [subst_def]
  \\ rename1 â€˜subst (f \\ _ \\ _)â€™
  \\ â€˜âˆ€v. v âˆˆ FRANGE f â‡’ closed vâ€™ by (rw [FRANGE_FLOOKUP])
  \\ drule $ GSYM subst_fdomsub
  \\ last_x_assum assume_tac
  \\ last_x_assum assume_tac
  \\ drule $ GSYM subst_fdomsub
  \\ rw [eval_Let, subst_def, bind1_def, IMP_closed_subst, DOMSUB_COMMUTES]
  \\ AP_TERM_TAC
  \\ rw [fmap_domsub, COMPL_UNION]
  \\ irule subst1_subst1
  \\ gvs [IMP_closed_subst]
QED

Theorem Let_Apps:
  âˆ€eL v e1 e2 b. (Let v e1 (Apps e2 eL) â‰…? Apps (Let v e1 e2) (MAP (Let v e1) eL)) b
Proof
  Induct >> rw [Apps_def, exp_eq_refl] >>
  irule exp_eq_trans >> pop_assum $ irule_at Any >>
  irule exp_eq_Apps_cong >> gvs [Let_App, exp_eq_l_refl]
QED

Theorem Seq_App:
  âˆ€b. (App (Seq p f) e â‰…? Seq p (App f e)) b
Proof
  irule eval_wh_IMP_exp_eq
  \\ rw [subst_def, eval_wh_App, eval_wh_Seq]
  \\ fs []
QED

Theorem Apps_Seq:
  âˆ€eL e e' b. (Apps (Seq e e') eL â‰…? Seq e (Apps e' eL)) b
Proof
  Induct >> rw [Apps_def, exp_eq_refl] >>
  irule exp_eq_trans >> pop_assum $ irule_at Any >>
  irule exp_eq_Apps_cong >> gvs [exp_eq_l_refl, Seq_App]
QED

Theorem Proj_Seq2:
  âˆ€b. (Proj x i (Seq e e') â‰…? Seq e (Proj x i e')) b
Proof
  irule eval_wh_IMP_exp_eq
  \\ rw [subst_def, eval_wh_Proj, eval_wh_Seq]
  \\ fs []
QED

Theorem If_Seq:
  âˆ€e e' e'' e''' b. (If (Seq e e') e'' e''' â‰…? Seq e (If e' e'' e''')) b
Proof
  rpt gen_tac
  \\ irule eval_wh_IMP_exp_eq
  \\ rw [subst_def, eval_wh_If, eval_wh_Seq]
  \\ fs []
QED

Theorem Seq_comm:
  Seq (Seq x y) z â‰ˆ Seq (Seq y x) z
Proof
  irule no_err_eval_wh_IMP_exp_eq
  \\ rw [subst_def, no_err_eval_wh_def, eval_wh_Seq]
  \\ fs []
  \\ Cases_on â€˜eval_wh (subst f x)â€™
  \\ Cases_on â€˜eval_wh (subst f y)â€™
  \\ fs []
QED

Theorem If_Seq2:
  âˆ€e ec et ee.  If ec (Seq e et) (Seq e ee) â‰ˆ Seq e (If ec et ee)
Proof
  rpt gen_tac
  \\ irule no_err_eval_wh_IMP_exp_eq
  \\ rw [no_err_eval_wh_def, freevars_def, subst_def, eval_wh_If, eval_wh_Seq]
  \\ fs []
QED

Theorem IsEq_Seq:
  âˆ€b e e' n i. (IsEq n i (Seq e e') â‰…? Seq e (IsEq n i e')) b
Proof
  rpt gen_tac
  \\ irule eval_wh_IMP_exp_eq
  \\ rw [subst_def, eval_wh_IsEq, eval_wh_Seq]
  \\ fs []
QED

Definition well_written_def:
  well_written (Cons s) l = T âˆ§
  well_written (Proj s i) [e] = T âˆ§
  well_written (IsEq s i) [e] = T âˆ§
  well_written If [e; e'; e''] = T âˆ§
  well_written Seq [e; e'] = T âˆ§
  well_written (AtomOp op) l = T âˆ§
  well_written _ _ = F
End

Theorem not_well_written_is_fail:
  âˆ€b ope l.
    Â¬ well_written ope l â‡’
    (Prim ope l â‰…? Fail) b
Proof
  rw []
  \\ Cases_on â€˜opeâ€™
  \\ fs [well_written_def]
  \\ Cases_on â€˜lâ€™
  >>~[â€˜Prim _ [] â‰…? Failâ€™]
  >- (fs [exp_eq_refl])
  >- (irule eval_wh_IMP_exp_eq
     \\ fs [subst_def, eval_wh_def, eval_wh_to_def])
  >- (irule eval_wh_IMP_exp_eq
     \\ fs [subst_def, eval_wh_def, eval_wh_to_def])
  >- (irule eval_wh_IMP_exp_eq
     \\ fs [subst_def, eval_wh_def, eval_wh_to_def])
  \\ rename1 â€˜hd::tlâ€™
  \\ Cases_on â€˜tlâ€™
  \\ fs [well_written_def]
  >>~[â€˜Prim _ [hd]â€™]
  >- (irule eval_wh_IMP_exp_eq
     \\ fs [subst_def, eval_wh_def, eval_wh_to_def])
  >- (irule eval_wh_IMP_exp_eq
     \\ fs [subst_def, eval_wh_def, eval_wh_to_def])
  \\ rename1 â€˜h0::h1::tlâ€™
  \\ Cases_on â€˜tlâ€™
  \\ fs [well_written_def]
  >~[â€˜Prim If (h0::h1::h2::tl)â€™]
  >- (Cases_on â€˜tlâ€™
      \\ fs [well_written_def]
      \\ irule eval_wh_IMP_exp_eq
      \\ fs [subst_def, eval_wh_def, eval_wh_to_def])
  \\ irule eval_wh_IMP_exp_eq
  \\ fs [subst_def, eval_wh_def, eval_wh_to_def]
QED

(*Theorem MAP_Pair:
  âˆ€l f1 f2. MAP (f1 ## f2) l = ZIP (MAP f1 (MAP FST l), MAP f2 (MAP SND l))
Proof
  Induct \\ gvs []
  \\ PairCases \\ fs []
QED

Theorem FOLDL_MAP:
  âˆ€l2 f hd l. FOLDL (Î»eL' (p1, p2). MAP (f p1 p2) eL') (hd::l) l2
              = (FOLDL (Î»e (p1, p2). f p1 p2 e) hd l2)::(FOLDL (Î»eL' (p1, p2). MAP (f p1 p2) eL') l l2)
Proof
  cheat
QED

Theorem FOLDL_MAP_Last:
  âˆ€l2 f hd l. FOLDL (Î»eL' (p1, p2). MAP (f p1 p2) eL') (l++[hd]) l2
              = (FOLDL (Î»eL' (p1, p2). MAP (f p1 p2) eL') l l2)++[FOLDL (Î»e (p1, p2). f p1 p2 e) hd l2]
Proof
  cheat
QED

Theorem ZIP_APPEND1:
  âˆ€l1 l2 l3 h1 h2. ZIP (l1, l2) ++ (h1, h2)::l3 = ZIP (l1 ++ [h1], l2 ++ [h2]) ++ l3
Proof
  cheat
QED

Theorem APPEND1:
  âˆ€l e. [e]++l = e::l
Proof
  cheat
QED

Theorem not_MEM_no_perm:
  âˆ€h h2 l. (Â¬MEM h (MAP FST l)) â‡’ MAP (perm1 h h2) (MAP FST l) = MAP FST l
Proof
  rw [] >> irule LIST_EQ >>
  rw [EL_MAP]
QED

Theorem Letrec_rename:
  âˆ€f1 f2 eL common e b.
    LENGTH f1 = LENGTH f2 âˆ§ LENGTH eL = LENGTH f1
    âˆ§ ALL_DISTINCT (f1 ++ MAP FST common)
    âˆ§ EVERY (Î»v. Â¬MEM v f1 âˆ§ v âˆ‰ freevars (Letrec (common ++ ZIP (f1, eL)) e)) f2
    â‡’ (Letrec (common ++ ZIP (f1, eL)) e â‰…?
       Letrec (ZIP (MAP FST common,
                    FOLDL (Î»eL' (x, y). MAP (perm_exp x y) eL') (MAP SND common) (ZIP (f1, f2)))
               ++ ZIP (f2,
                       FOLDL (Î»eL' (x, y). MAP (perm_exp x y) eL') eL (ZIP (f1, f2))))
       (FOLDL (Î»e (x, y). perm_exp x y e) e (ZIP (f1, f2)))
      ) b
Proof
  Induct
  >- gvs [exp_eq_refl, GSYM UNZIP_MAP, ZIP_UNZIP] >>
  gen_tac >> Cases >> Cases >>
  rw [FOLDL_MAP, ZIP_APPEND1, GSYM FOLDL_MAP_Last] >>
  irule exp_eq_trans >> irule_at (Pos hd) exp_alpha_exp_eq >>
  irule_at Any exp_alpha_Letrec_Alpha >>
  rename1 â€˜h2 â‰  hâ€™ >> qexists_tac â€˜h2â€™ >> fs [] >>
  rename1 â€˜MAP (perm1 h h2 ## perm_exp _ _) (ZIP (f1, eL))â€™ >>
  rename1 â€˜EVERY _ f2â€™ >>
  last_x_assum $ qspecl_then
               [â€˜f2â€™, â€˜eLâ€™, â€˜(MAP (perm1 h h2 ## perm_exp h h2) common) ++ [h2, perm_exp h h2 h'']â€™]
               assume_tac >>
  gvs [MAP_Pair, APPEND1, MAP_ZIP]
  gvs [MAP_Pair, ZIP_APPEND1] >>
  rename1 â€˜Letrec (common ++ (hv1, eL1)::ZIP(f1, tL))â€™ >>
  qspecl_then [â€˜commonâ€™, â€˜ZIP (f1, tL)â€™, â€˜hv1â€™, â€˜h'â€™, â€˜eL1â€™] assume_tac exp_alpha_Letrec_Alpha >>
  cheat
QED

Theorem Letrec_renameList:
  âˆ€s bL e b. âˆƒbL' e'. set (MAP FST bL') âˆ© s = {} âˆ§ (Letrec bL e â‰…? Letrec bL' e') b
Proof
  cheat
QED*)

(* Part on context *)

Datatype:
  ctxt = Nil
       | IsFree string ctxt
       | Bind string exp ctxt
       | RecBind ((string # exp) list) ctxt
End

Definition exp_eq_in_ctxt_def:
  exp_eq_in_ctxt Nil = (Î»e1 e2. e1 â‰ˆ e2) âˆ§
  exp_eq_in_ctxt (IsFree s c) e1 e2 = (âˆ€e3. closed e3 â‡’ exp_eq_in_ctxt c (Let s e3 e1) (Let s e3 e2)) âˆ§
  exp_eq_in_ctxt (Bind s e3 c) e1 e2 = exp_eq_in_ctxt c (Let s e3 e1) (Let s e3 e2) âˆ§
  exp_eq_in_ctxt (RecBind l c) e1 e2 = exp_eq_in_ctxt c (Letrec l e1) (Letrec l e2)
End

Definition ctxt_size_def:
  ctxt_size Nil = 0n âˆ§
  ctxt_size (IsFree s ctxt) = 1 + ctxt_size ctxt âˆ§
  ctxt_size (Bind s e ctxt) = 1 + list_size char_size s +  exp_size e + ctxt_size ctxt âˆ§
  ctxt_size (RecBind sel ctxt) = 1 + exp1_size sel + ctxt_size ctxt
End

Theorem exp_eq_in_ctxt_refl:
  âˆ€c e. exp_eq_in_ctxt c e e
Proof
  Induct
  \\ gvs [exp_eq_in_ctxt_def, exp_eq_refl]
QED

Theorem exp_eq_in_ctxt_l_refl:
  âˆ€c l. LIST_REL (exp_eq_in_ctxt c) l l
Proof
  gen_tac
  \\ Induct
  \\ fs [exp_eq_in_ctxt_refl]
QED

Theorem exp_eq_in_ctxt_sym:
  âˆ€c e1 e2. exp_eq_in_ctxt c e1 e2 â‡” exp_eq_in_ctxt c e2 e1
Proof
  Induct
  \\ rw [] \\ eq_tac
  \\ gvs [exp_eq_in_ctxt_def, exp_eq_sym]
QED

Theorem exp_eq_in_ctxt_trans:
  âˆ€c e1 e2 e3. exp_eq_in_ctxt c e1 e2 âˆ§ exp_eq_in_ctxt c e2 e3 â‡’ exp_eq_in_ctxt c e1 e3
Proof
  Induct
  \\ rw []
  \\ gvs [exp_eq_in_ctxt_def]
  >- (irule exp_eq_trans
      \\ pop_assum $ irule_at Any \\ fs [])
  \\ rw []
  \\ last_x_assum irule
  \\ first_x_assum $ irule_at Any
  \\ gvs []
QED

Theorem exp_eq_IMP_exp_eq_in_ctxt:
  âˆ€c e e'. e â‰ˆ e' â‡’ exp_eq_in_ctxt c e e'
Proof
  Induct
  \\ gvs [exp_eq_in_ctxt_def]
  \\ rw []
  \\ first_x_assum irule
  \\ gvs [exp_eq_Letrec_cong, exp_eq_App_cong, exp_eq_Lam_cong, exp_eq_refl, exp_eq_l_refl]
QED

Theorem exp_eq_in_ctxt_Prim:
  âˆ€c eL eL' ope. LIST_REL (exp_eq_in_ctxt c) eL eL' â‡’ exp_eq_in_ctxt c (Prim ope eL) (Prim ope eL')
Proof
  Induct
  \\ gvs [exp_eq_in_ctxt_def, exp_eq_refl, exp_eq_Prim_cong]
  \\ rw []
  \\ irule exp_eq_in_ctxt_trans
  \\ irule_at Any exp_eq_in_ctxt_trans
  \\ last_x_assum $ irule_at Any
  \\ irule_at (Pos last) exp_eq_IMP_exp_eq_in_ctxt
  \\ irule_at (Pos last) exp_eq_IMP_exp_eq_in_ctxt
  >~[â€˜Letrecâ€™]
  >- (irule_at Any Letrec_Prim
      \\ irule_at Any $ iffLR exp_eq_sym
      \\ irule_at Any Letrec_Prim
      \\ gvs [LIST_REL_EL_EQN, EL_MAP, exp_eq_in_ctxt_def])
  \\ irule_at Any Let_Prim
  \\ irule_at Any $ iffLR exp_eq_sym
  \\ irule_at Any Let_Prim
  \\ gvs [LIST_REL_EL_EQN, EL_MAP, exp_eq_in_ctxt_def]
QED

Theorem Let_App_in_ctxt:
  âˆ€c s e1 e2 e3. exp_eq_in_ctxt c (Let s e1 (App e2 e3)) (App (Let s e1 e2) (Let s e1 e3))
Proof
  rpt gen_tac
  \\ irule exp_eq_IMP_exp_eq_in_ctxt
  \\ gvs [Let_App]
QED

Theorem exp_eq_in_ctxt_App:
  âˆ€c f1 f2 e1 e2. exp_eq_in_ctxt c f1 f2 âˆ§ exp_eq_in_ctxt c e1 e2
                  â‡’ exp_eq_in_ctxt c (App f1 e1) (App f2 e2)
Proof
  Induct
  \\ gvs [exp_eq_in_ctxt_def,exp_eq_App_cong]
  \\ rw []
  \\ irule exp_eq_in_ctxt_trans
  \\ irule_at Any exp_eq_in_ctxt_trans
  \\ last_x_assum $ irule_at (Pos $ el 2)
  \\ first_x_assum $ irule_at (Pos $ el 2)
  \\ rpt $ last_assum $ irule_at Any
  \\ irule_at (Pos last) $ iffLR exp_eq_in_ctxt_sym
  \\ rpt $ irule_at Any Let_App_in_ctxt
  \\ fs [Letrec_App, exp_eq_IMP_exp_eq_in_ctxt]
QED

Theorem exp_eq_in_ctxt_Apps:
  âˆ€eL eL' e e' c. LIST_REL (exp_eq_in_ctxt c) eL eL' â‡’ exp_eq_in_ctxt c e e'
                  â‡’ exp_eq_in_ctxt c (Apps e eL) (Apps e' eL')
Proof
  Induct
  >- (Cases
      \\ fs [Apps_def])
  \\ gen_tac
  \\ Cases
  \\ rw [Apps_def]
  \\ first_x_assum irule
  \\ fs [exp_eq_in_ctxt_App]
QED

Theorem Let_freevars:
  âˆ€c v e e'. v âˆ‰ freevars e' â‡’ exp_eq_in_ctxt c (Let v e e') e'
Proof
  rw []
  \\ irule exp_eq_IMP_exp_eq_in_ctxt
  \\ irule eval_wh_IMP_exp_eq
  \\ rw [subst_def, eval_wh_thm, bind1_def, GSYM subst_fdomsub]
  >- (AP_TERM_TAC
      \\ irule subst1_ignore
      \\ rename1 â€˜subst f e2â€™
      \\ qsuff_tac â€˜closed (subst f e2)â€™
      >- rw [closed_def]
      \\ irule IMP_closed_subst
      \\ rw []
      \\ first_x_assum $ irule_at Any
      \\ fs [FRANGE_FLOOKUP]
      \\ pop_assum $ irule_at Any)
  \\ gvs [IMP_closed_subst, FRANGE_FLOOKUP]
QED

Theorem Let_Let_in_ctxt:
  âˆ€v w e1 e2 e3 c. v â‰  w âˆ§ v âˆ‰ freevars e2 âˆ§ w âˆ‰ freevars e1 â‡’
                   exp_eq_in_ctxt c (Let v e1 (Let w e2 e3)) (Let w e2 (Let v e1 e3))
Proof
  rw []
  \\ irule exp_eq_IMP_exp_eq_in_ctxt
  \\ gvs [Let_Let]
QED

Theorem exp_eq_in_ctxt_Lam:
  âˆ€c s e1 e2. exp_eq_in_ctxt (IsFree s c) e1 e2
              â‡’ exp_eq_in_ctxt c (Lam s e1) (Lam s e2)
Proof
  Induct
  \\ fs[exp_eq_in_ctxt_def] \\ rw [exp_eq_in_ctxt_def]
  >- (rw [exp_eq_Lam_strong]
      \\ irule exp_eq_trans
      \\ irule_at Any beta_equality
      \\ fs []
      \\ irule $ iffLR exp_eq_sym
      \\ irule exp_eq_trans
      \\ irule_at (Pos last) beta_equality
      \\ fs [exp_eq_sym])
  >>~ [â€˜Letrec l (Lam w _)â€™]
  >- (â€˜âˆƒs. s âˆ‰ {w} âˆª set (MAP FST l) âˆª BIGUNION (set (MAP (freevars o SND) l))
             âˆª freevars e1 âˆª freevars e2â€™
        by  (â€˜INFINITE ð•Œ(:string)â€™ by simp [] \\ dxrule_then assume_tac $ iffLR NOT_IN_FINITE
             \\ pop_assum $ irule_at Any \\ rw [FINITE_UNION, FINITE_BIGUNION, MEM_EL]
             \\ gvs [EL_MAP])
      \\ irule exp_eq_in_ctxt_trans
      \\ irule_at (Pos hd) exp_eq_IMP_exp_eq_in_ctxt
      \\ irule_at Any exp_eq_Letrec_cong
      \\ irule_at Any exp_eq_l_refl
      \\ irule_at Any exp_alpha_exp_eq
      \\ irule_at Any exp_alpha_Alpha
      \\ fs [] \\ first_assum $ irule_at Any
      \\ fs []
      \\ irule exp_eq_in_ctxt_trans
      \\ irule_at (Pos hd) exp_eq_IMP_exp_eq_in_ctxt
      \\ irule_at Any Letrec_Lam_weak
      \\ fs [] \\ conj_tac
      >- (rw [EVERY_MEM]
          \\ rename1 â€˜MEM e _â€™ \\ last_x_assum $ qspecl_then [â€˜freevars eâ€™] assume_tac
          \\ fs [MEM_MAP]
          \\ rename1 â€˜e = SND pairâ€™ \\ pop_assum $ qspecl_then [â€˜pairâ€™] assume_tac
          \\ rw [])
      \\ irule exp_eq_in_ctxt_trans
      \\ irule_at (Pos last) exp_eq_IMP_exp_eq_in_ctxt
      \\ irule_at Any $ iffLR exp_eq_sym
      \\ irule_at Any exp_eq_Letrec_cong
      \\ irule_at Any exp_alpha_exp_eq
      \\ irule_at Any exp_alpha_Alpha
      \\ irule_at Any exp_eq_l_refl
      \\ first_assum $ irule_at Any \\ fs []
      \\ irule exp_eq_in_ctxt_trans
      \\ irule_at (Pos last) exp_eq_IMP_exp_eq_in_ctxt
      \\ irule_at Any $ iffLR exp_eq_sym
      \\ irule_at Any Letrec_Lam_weak
      \\ fs []
      \\ conj_tac
      >- (rw [EVERY_MEM]
          \\ rename1 â€˜MEM e _â€™ \\ last_x_assum $ qspecl_then [â€˜freevars eâ€™] assume_tac
          \\ fs [MEM_MAP]
          \\ rename1 â€˜e = SND pairâ€™ \\ pop_assum $ qspecl_then [â€˜pairâ€™] assume_tac
          \\ rw [])
      \\ last_x_assum irule \\ rw []
      \\ irule exp_eq_in_ctxt_trans
      \\ irule_at Any exp_eq_in_ctxt_trans
      \\ last_x_assum $ irule_at Any
      \\ irule_at (Pos last) $ iffLR exp_eq_in_ctxt_sym
      \\ rename1 â€˜Let s e3 _â€™ \\ qexists_tac â€˜e3â€™ \\ fs []
      \\ conj_tac
      \\ irule exp_eq_IMP_exp_eq_in_ctxt
      \\ irule exp_eq_trans
      \\ irule_at Any Let_Letrec
      \\ fs []
      \\ irule_at Any exp_eq_Letrec_cong
      \\ irule_at Any $ iffLR exp_eq_sym
      \\ irule_at Any exp_eq_App_cong
      \\ irule_at Any exp_alpha_exp_eq
      \\ irule_at Any exp_alpha_Alpha
      \\ rw [EVERY_MEM, exp_eq_refl, exp_eq_l_refl] \\ fs [closed_def]
      \\ rename1 â€˜MEM e _â€™
      \\ last_x_assum $ qspecl_then [â€˜freevars eâ€™] assume_tac
      \\ fs [MEM_MAP]
      \\ rename1 â€˜e = SND pairâ€™ \\ pop_assum $ qspecl_then [â€˜pairâ€™] assume_tac
      \\ rw [])
  \\ rename1 â€˜Let v e3 (Lam w _)â€™
  \\ â€˜âˆƒs. s âˆ‰ {v} âˆª {w} âˆª freevars e3 âˆª freevars e1 âˆª freevars e2â€™
    by (â€˜INFINITE ð•Œ(:string)â€™ by simp []
        \\ gvs [NOT_IN_FINITE])
  \\ irule exp_eq_in_ctxt_trans
  \\ irule_at (Pos hd) exp_eq_IMP_exp_eq_in_ctxt
  \\ irule_at Any exp_eq_App_cong \\ irule_at Any exp_eq_Lam_cong
  \\ irule_at Any exp_alpha_exp_eq
  \\ irule_at Any exp_alpha_Alpha
  \\ fs [] \\ first_assum $ irule_at Any
  \\ irule_at Any exp_eq_refl \\ fs []
  \\ irule exp_eq_in_ctxt_trans
  \\ irule_at (Pos hd) exp_eq_IMP_exp_eq_in_ctxt
  \\ irule_at Any Let_Lam_weak \\ fs []
  \\ irule exp_eq_in_ctxt_trans
  \\ irule_at (Pos last) exp_eq_IMP_exp_eq_in_ctxt
  \\ irule_at Any $ iffLR exp_eq_sym
  \\ irule_at Any exp_eq_App_cong \\ irule_at Any exp_eq_Lam_cong
  \\ irule_at Any exp_alpha_exp_eq
  \\ irule_at Any exp_alpha_Alpha
  \\ first_assum $ irule_at Any
  \\ irule_at Any exp_eq_refl \\ fs []
  \\ irule exp_eq_in_ctxt_trans
  \\ irule_at (Pos last) exp_eq_IMP_exp_eq_in_ctxt
  \\ irule_at Any $ iffLR exp_eq_sym
  \\ irule_at Any Let_Lam_weak \\ fs []
  \\ last_x_assum $ irule_at Any
  \\ rw []
  \\ irule exp_eq_in_ctxt_trans \\ irule_at Any exp_eq_in_ctxt_trans
  \\ last_x_assum $ irule_at Any
  \\ rpt $ irule_at Any exp_eq_IMP_exp_eq_in_ctxt
  \\ irule_at (Pos $ el 2) $ iffLR exp_eq_sym
  \\ irule_at (Pos $ el 2) exp_eq_trans \\ irule_at (Pos $ el 3) exp_eq_trans
  \\ irule_at (Pos $ el 2) Let_Let \\ irule_at (Pos $ el 6) Let_Let
  \\ rpt $ irule_at Any exp_eq_App_cong
  \\ rpt $ irule_at Any exp_eq_refl
  \\ rpt $ irule_at Any exp_eq_Lam_cong
  \\ rpt $ irule_at Any exp_eq_App_cong
  \\ rpt $ irule_at Any exp_eq_refl
  \\ fs [closed_def] \\ conj_tac
  \\ irule exp_alpha_exp_eq
  \\ irule exp_alpha_Alpha
  \\ fs []
QED

Theorem exp_eq_in_ctxt_Lams:
  âˆ€vl c e e'. exp_eq_in_ctxt (FOLDL (Î»c n. IsFree n c) c vl) e e' â‡’
              exp_eq_in_ctxt c (Lams vl e) (Lams vl e')
Proof
  Induct
  \\ rw [Lams_def]
  \\ irule exp_eq_in_ctxt_Lam
  \\ last_x_assum $ irule_at Any
  \\ fs []
QED

Theorem exp_eq_in_IsFree_Bind:
  âˆ€e1 e2 e3 c v. exp_eq_in_ctxt (IsFree v c) e1 e2 â‡’ exp_eq_in_ctxt (Bind v e3 c) e1 e2
Proof
  rpt strip_tac >>
  gvs [exp_eq_in_ctxt_def, exp_eq_in_ctxt_App, exp_eq_in_ctxt_Lam, exp_eq_in_ctxt_refl]
QED

Theorem Apps_Seq_in_ctxt:
  âˆ€e e2 eL c. exp_eq_in_ctxt c (Apps (Seq e e2) eL) (Seq e (Apps e2 eL))
Proof
  rw [exp_eq_IMP_exp_eq_in_ctxt, Apps_Seq]
QED

Theorem Let_Apps_in_ctxt:
  âˆ€v e1 e2 eL c. exp_eq_in_ctxt c (Let v e1 (Apps e2 eL)) (Apps (Let v e1 e2) (MAP (Let v e1) eL))
Proof
  rw [Let_Apps, exp_eq_IMP_exp_eq_in_ctxt]
QED


val _ = export_theory();
